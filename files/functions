#!/bin/zsh
join-lines() {
  local item
  while read item; do
    echo -n "${(q)item} "
  done
}

# Git
cg () {
    is-git-repo || echo 'Not in git repo'
    cd $(git rev-parse --show-toplevel)
}

is-git-repo() {
    git rev-parse HEAD > /dev/null 2>&1
}

gpush() {
    is-git-repo || echo 'Not in git repo'

    cur_branch=$(git branch --show-current)
    has_remote=$(git branch -r --list "origin/$cur_branch")
    if [[ $has_remote ]]; then
        git push "$@"
    else
        echo "No remote branch found, creating one"
        git push --set-upstream origin "$cur_branch" "$@"
    fi
}

zle -N ctrl-t-widget
ctrl-t-widget() {
    # If the current command is 'git add ' or 'git restore ', fuzzy find changed files
    if [[ $LBUFFER =~ '^git (add|restore|checkout) ' ]]; then
        result=$(fzf-git-changed | join-lines)
        zle reset-prompt
        LBUFFER+=$result
    # Find only '.spec' files
    elif [[ $LBUFFER =~ '^(yarn|pnpm) test ' ]]; then
        result=$(fzf-js-test | join-lines)
        zle reset-prompt
        LBUFFER+=$result
    # Otherwise do default and fuzzy search all files
    else
        LBUFFER+=$(eval $FZF_CTRL_T_COMMAND | eval "fzf $FZF_CTRL_T_OPTS")
    fi
}
bindkey '' ctrl-t-widget

gstash() {
    is-git-repo || echo 'Not in git repo'

    local stash=$(fzf-git-stash)
    git stash pop "$stash"
}

# FZF
fzf-js-test() {
    cmd="$FZF_CTRL_T_COMMAND '.spec.(t|j)s$'"
    eval "$cmd" |
    fzf \
        --multi \
        --ansi \
        --height 100% \
        --layout reverse \
        --header 'ctrl-f: All files / ctrl-g Test files' \
        --bind "ctrl-f:change-prompt(Files> )+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-prompt(Test Files> )+reload($cmd)" \
        --prompt "Test Files> " \
        --preview "bat --color=always --line-range=:40 --style=plain --highlight-line=\"\$(cat {} | grep -m1 -n 'describe' | cut -f1 -d:)\" {}"
}

fzf-git-checkout() {
    is-git-repo || echo 'Not in git repo'

    # pass args straight to git-checkout if provided
    arg=$1
    if [[ -n $arg ]]; then
        git checkout "$@"
        return
    fi

    git branch --all --color=always --sort=-committerdate | grep -v HEAD | sed 's/^..//' | cut -d' ' -f1 |
    fzf-tmux \
        -p 50%,50%\
        --ansi \
        --header "Enter (checkout) / Ctrl-a (checkout -b) / Ctrl-t (checkout --track) / Ctrl-d (delete)" \
        --bind "enter:execute(git checkout {..})+accept" \
        --bind "ctrl-a:execute(git checkout -b {q})+accept" \
        --bind "ctrl-t:execute(git checkout --track {..})+accept" \
        --bind "ctrl-d:execute(git branch -d {..})+accept" \
        --prompt "Braches> " \
        --preview-window right:50% \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}'
}

fzf-git-rebase() {
    is-git-repo || echo 'Not in git repo'

    cmd="git log --date-order --abbrev-commit --oneline"
    eval "$cmd" |
    fzf \
        --ansi \
        --height 100% \
        --layout reverse \
        --prompt "Commits> " \
        --preview-window right:50% \
        --preview 'git show --color=always --format=full --stat -p {1}' |
    cut -d' ' -f1
}

zle -N git-branch-widget
fzf-git-branch() {
    is-git-repo || echo 'Not in git repo'

    git branch --all --color=always --sort=-committerdate | grep -v HEAD | sed 's/^..//' | cut -d' ' -f1 |
    fzf-tmux \
        -p 50%,50%\
        --ansi \
        --prompt "Braches> " \
        --preview-window right:50% \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}'
}
git-branch-widget() {
    result=$(fzf-git-branch | join-lines)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' git-branch-widget

fzf-git-changed() {
    is-git-repo || echo 'Not in git repo'
    cmd="git -c color.status=always status --short"

    eval "$cmd" |
    fzf \
        --multi \
        --ansi \
        --height 40% \
        --layout reverse \
        --nth 2..,.. \
        --header 'ctrl-f: All files / ctrl-g: Changed files' \
        --bind "ctrl-f:change-prompt(Files> )+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-prompt(Changed Files> )+reload($cmd)" \
        --prompt "Changed Files> " \
        --preview '(git diff --color=always -- {-1} | sed 1,4d; cat {-1})' |
    sed 's/.* //'
}

fzf-git-stash() {
    is-git-repo || echo 'Not in git repo'

    FZF_DEFAULT_COMMAND='git stash list'
    fzf \
        --ansi \
        --reverse \
        --no-multi \
        -d: \
        --header='Enter (apply) / Ctrl-d (drop)' \
        --bind='enter:execute(git stash apply --quiet {..})+accept' \
        --bind='ctrl-d:execute(git stash drop --quiet {..})+accept' \
        --preview='git stash show --color=always --stat {1}' |
    cut -d: -f1
}

fzf-ls() {
    FZF_DEFAULT_COMMAND='fd -tf'
    fzf \
        --ansi \
        --multi \
        --header "ctrl-d: Directories / ctrl-f: Files" \
        --bind "ctrl-d:change-prompt(Directories> )+reload(fd -td)" \
        --bind "ctrl-f:change-prompt(Files> )+reload(fd -tf)" \
        --prompt "Files> "
}

fzf-cd() {
    fd -td -H -E '*.git' |
    fzf \
        --ansi \
        --no-multi \
        --prompt "Directories> "
}

# Others
source-envfile() {
    set -a
    source "$1"
    set +a
}

zle -N dcd
dcd() {
    result=$(fzf-cd)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' dcd
